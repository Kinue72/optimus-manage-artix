pkgname=optimus-manager-artix-git
pkgver=1.4.r12.g29d737f
pkgrel=1
pkgdesc="Management utility to handle GPU switching for Optimus laptops (Git version)"
arch=('any')
url="https://github.com/Askannz/optimus-manager"
license=('MIT')
conflicts=("optimus-manager" "optimus-manager-git")
provides=("optimus-manager=$pkgver")
depends=('python3' 'python-setuptools' 'python-dbus' 'glxinfo' 'xorg-xrandr')
optdepends=('bbswitch: alternative power switching method'
            'acpi_call: alternative power switching method'
            'xf86-video-intel: provides the Xorg intel driver')
makedepends=('python-setuptools' 'git')
backup=('etc/optimus-manager/xorg/integrated-mode/integrated-gpu.conf'
        'etc/optimus-manager/xorg/nvidia-mode/integrated-gpu.conf'
        'etc/optimus-manager/xorg/nvidia-mode/nvidia-gpu.conf'
        'etc/optimus-manager/xorg/hybrid-mode/integrated-gpu.conf'
        'etc/optimus-manager/xorg/hybrid-mode/nvidia-gpu.conf'

        'etc/optimus-manager/xsetup-integrated.sh'
        'etc/optimus-manager/xsetup-nvidia.sh'
        'etc/optimus-manager/xsetup-hybrid.sh'

        'etc/optimus-manager/nvidia-enable.sh'
        'etc/optimus-manager/nvidia-disable.sh'

        'var/lib/optimus-manager/persistent/startup_mode')
source=("git+https://github.com/Askannz/optimus-manager.git#branch=master"
		"https://github.com/Kinue72/optimus-manage-artix/commit/55bcb54fea877bd133434d0fc1d38908bb838e7c.patch"
		"https://github.com/Kinue72/optimus-manage-artix/commit/d55df8b87aed7661cdb1faca5579af1405524963.patch")
sha256sums=('SKIP'
			'SKIP'
			'SKIP')

prepare() {
    cd "${srcdir}/optimus-manager/"
    git apply ../55bcb54fea877bd133434d0fc1d38908bb838e7c.patch
    git apply ../d55df8b87aed7661cdb1faca5579af1405524963.patch
}

pkgver() {
  cd "${srcdir}/optimus-manager/"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}
 
build() {
 
  cd "${srcdir}/optimus-manager/"
  /usr/bin/python3 setup.py build
 
}
 
 
package() {

  install="optimus-manager.install"
 
  cd "${srcdir}/optimus-manager/"
 
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 modules/optimus-manager.conf "$pkgdir/usr/lib/modprobe.d/optimus-manager.conf"
  install -Dm644 systemd/optimus-manager.service "$pkgdir/usr/lib/systemd/system/optimus-manager.service"
  install -Dm644 optimus-manager.conf "$pkgdir/usr/share/optimus-manager.conf"
  
  install -Dm644 systemd/logind/10-optimus-manager.conf "$pkgdir/usr/lib/systemd/logind.conf.d/10-optimus-manager.conf"
  install -Dm755 systemd/suspend/optimus-manager.py "$pkgdir/usr/lib/systemd/system-sleep/optimus-manager.py"
  
  
  install -Dm644 login_managers/sddm/20-optimus-manager.conf "$pkgdir/etc/sddm.conf.d/20-optimus-manager.conf"
  install -Dm644 login_managers/lightdm/20-optimus-manager.conf  "$pkgdir/etc/lightdm/lightdm.conf.d/20-optimus-manager.conf"
  
  install -Dm644 config/xorg/integrated-mode/integrated-gpu.conf "$pkgdir/etc/optimus-manager/xorg/integrated-mode/integrated-gpu.conf"
  install -Dm644 config/xorg/nvidia-mode/nvidia-gpu.conf "$pkgdir/etc/optimus-manager/xorg/nvidia-mode/nvidia-gpu.conf"
  install -Dm644 config/xorg/nvidia-mode/integrated-gpu.conf "$pkgdir/etc/optimus-manager/xorg/nvidia-mode/integrated-gpu.conf"
  install -Dm644 config/xorg/hybrid-mode/integrated-gpu.conf "$pkgdir/etc/optimus-manager/xorg/hybrid-mode/integrated-gpu.conf"
  install -Dm644 config/xorg/hybrid-mode/nvidia-gpu.conf "$pkgdir/etc/optimus-manager/xorg/hybrid-mode/nvidia-gpu.conf"
  
  install -Dm755 config/xsetup-nvidia.sh "$pkgdir/etc/optimus-manager/xsetup-nvidia.sh"
  install -Dm755 config/xsetup-hybrid.sh "$pkgdir/etc/optimus-manager/xsetup-hybrid.sh"
  install -Dm755 config/xsetup-integrated.sh "$pkgdir/etc/optimus-manager/xsetup-integrated.sh"

  install -Dm755 config/nvidia-enable.sh "$pkgdir/etc/optimus-manager/nvidia-enable.sh"
  install -Dm755 config/nvidia-disable.sh "$pkgdir/etc/optimus-manager/nvidia-disable.sh"
 
  /usr/bin/python3 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
 
} 
